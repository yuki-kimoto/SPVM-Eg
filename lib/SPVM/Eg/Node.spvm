# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Eg::Node {
  allow Eg;
  
  use Hash;
  use List;
  use StringBuffer;
  use Eg::Style;
  
  enum {
    TYPE_ELEMENT_NODE = 1,
    TYPE_ATTRIBUTE_NODE = 2,
    TYPE_TEXT_NODE = 3,
    TYPE_CDATA_SECTION_NODE = 4,
    TYPE_PROCESSING_INSTRUCTION_NODE = 7,
    TYPE_COMMENT_NODE = 8,
    TYPE_DOCUMENT_NODE = 9,
    TYPE_DOCUMENT_TYPE_NODE = 10,
    TYPE_DOCUMENT_FRAGMENT_NODE = 11,
  }
  
  # Fields
  has type : ro int;
  
  has nodes : ro List of Eg::Node;
  
  has attributes_h : Hash;
  
  has style : rw Eg::Style;
  
  has tag_name : ro string;
  
  has text_buffer : ro StringBuffer;
  
  has parent : Eg::Node;
  
  # Class Methods
  static method new : Eg::Node ($type : int, $nodes : Eg::Node[] = undef) {
    
    my $self = new Eg::Node;
    
    $self->{type} = $type;
    
    $self->{nodes} = List->new($nodes);
    
    if ($nodes) {
      for my $node (@$nodes) {
        $node->{parent} = $self;
        weaken $node->{parent};
      }
    }
    
    return $self;
  }
  
  static method new_document : Eg::Node ($root_node : Eg::Node) {
    
    my $self = Eg::Node->new(Eg::Node->TYPE_DOCUMENT_NODE, [$root_node]);
    
    return $self;
  }
  
  static method new_element : Eg::Node ($tag_name : string, $attribute_pairs : object[] = undef, $nodes : Eg::Node[] = undef) {
    
    unless ($tag_name) {
      die "\$tag_namet must be defined.";
    }
    
    my $self = Eg::Node->new(Eg::Node->TYPE_ELEMENT_NODE, $nodes);
    
    $self->{tag_name} = $tag_name;
    
    if ($attribute_pairs) {
      $self->{attributes_h} = Hash->new($attribute_pairs);
      
      my $attributes_h = $self->{attributes_h};
      
      my $style_pairs = (object[])$attributes_h->delete("style");
      
      my $style = Eg::Style->new($style_pairs);
      
      $self->{style} = $style;
    }
    
    return $self;
  }
  
  static method new_text : Eg::Node ($text : string) {
    
    unless ($text) {
      die "\$text must be defined.";
    }
    
    my $self = Eg::Node->new(Eg::Node->TYPE_TEXT_NODE);
    
    my $text_buffer = StringBuffer->new;
    
    $text_buffer->push($text);
    
    $self->{text_buffer} = $text_buffer;
    
    return $self;
  }
  
  # Instance Methods
  method css : string ($name : string) {
    
    my $style = $self->{style};
    
    return $style->{style_h}->get_string($name);
  }
  
  method set_css : void ($name : string, $value : string) {
    
    my $style = $self->{style};
    
    $style->{style_h}->set_string($name, $value);
  }
  
  method attr : string ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($self->{attributes_h}) {
      return undef;
    }
    
    my $attr = (string)$attributes_h->get($name);
    
    return $attr;
  }
  
  method set_attr : void ($name : string, $value : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($attributes_h) {
      $attributes_h = $self->{attributes_h} = Hash->new;
    }
    
    $attributes_h->set_string($name, $value);
  }
  
  method exists_attr : int ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    my $exists = 0;
    if ($attributes_h) {
      $exists = $attributes_h->exists($name);
    }
    
    return $exists;
  }
  
  method remove_attr : void ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    my $deleted_attr = (string)undef;
    
    if ($self->{attributes_h}) {
      $attributes_h->delete($name);
    }
  }
  
  method attrs : string[] () {
    
    my $attributes_h = $self->{attributes_h};
    
    my $attrs = (string[])undef;
    
    if ($attributes_h) {
      my $names = $attributes_h->keys;
      
      Sort->sort_string_asc($names);
      
      my $attributes_pairs_list = StringList->new;
      for my $name (@$names) {
        my $value = $attributes_h->get_string($name);
        
        $attributes_pairs_list->push($name);
        $attributes_pairs_list->push($value);
      }
      
      $attrs = $attributes_pairs_list->to_array;
    }
    else {
      $attrs = new string[0];
    }
    
    return $attrs;
  }
  
  method text : string () {
    my $nodes = $self->{nodes};
    
    my $nodes_length = $nodes->length;
    
    my $text = (string)undef;
    
    if ($nodes_length > 0) {
      my $first_child = (Eg::Node)$nodes->get(0);
      
      if ($first_child->type == &TYPE_TEXT_NODE) {
        $text = $first_child->{text_buffer}->to_string;
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      return "";
    }
    
    return $text;
  }
  
  method set_text : string ($text : string) {
    my $nodes = $self->{nodes};
    
    my $nodes_length = $nodes->length;
    
    if ($nodes_length > 0) {
      my $first_child = (Eg::Node)$nodes->get(0);
      
      if ($first_child->type == &TYPE_TEXT_NODE) {
        $first_child->{text_buffer}->set($text);
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      $self->{nodes} = List->new([&new_text($text)]);
    }
    
  }
  
}
