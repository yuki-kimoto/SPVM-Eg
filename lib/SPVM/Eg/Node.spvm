# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Eg::Node {
  allow Eg;
  
  use Hash;
  use List;
  use StringBuffer;
  use Eg::Style;
  
  enum {
    TYPE_ELEMENT_NODE = 1,
    TYPE_ATTRIBUTE_NODE = 2,
    TYPE_TEXT_NODE = 3,
    TYPE_CDATA_SECTION_NODE = 4,
    TYPE_PROCESSING_INSTRUCTION_NODE = 7,
    TYPE_COMMENT_NODE = 8,
    TYPE_DOCUMENT_NODE = 9,
    TYPE_DOCUMENT_TYPE_NODE = 10,
    TYPE_DOCUMENT_FRAGMENT_NODE = 11,
  }
  
  # Fields
  has type : ro int;
  
  has nodes_list : ro List of Eg::Node;
  
  has attributes_h : ro Hash of string;
  
  has styles_h : ro Hash of string;
  
  has tag_name : ro string;
  
  has value_buffer : ro StringBuffer;
  
  has parent : ro Eg::Node;
  
  # Class Methods
  static method new : Eg::Node ($type : int, $nodes : Eg::Node[] = undef) {
    
    my $self = new Eg::Node;
    
    $self->{type} = $type;
    
    $self->{nodes_list} = List->new($nodes);
    
    if ($nodes) {
      for my $node (@$nodes) {
        $node->{parent} = $self;
        weaken $node->{parent};
      }
    }
    
    return $self;
  }
  
  static method new_document : Eg::Node ($root_node : Eg::Node) {
    
    my $self = Eg::Node->new(Eg::Node->TYPE_DOCUMENT_NODE, [$root_node]);
    
    return $self;
  }
  
  static method new_element : Eg::Node ($tag_name : string, $attribute_pairs : object[] = undef, $nodes : Eg::Node[] = undef) {
    
    unless ($tag_name) {
      die "\$tag_namet must be defined.";
    }
    
    my $self = Eg::Node->new(Eg::Node->TYPE_ELEMENT_NODE, $nodes);
    
    $self->{tag_name} = $tag_name;
    
    if ($attribute_pairs) {
      $self->{attributes_h} = Hash->new($attribute_pairs);
      
      my $attributes_h = $self->{attributes_h};
      
      my $styles_pairs = (object[])$attributes_h->delete("style");
      
      if ($styles_pairs) {
        my $styles_h = Hash->new($styles_pairs);
        
        $self->{styles_h} = $styles_h;
      }
    }
    
    return $self;
  }
  
  static method new_text : Eg::Node ($text : string) {
    
    unless ($text) {
      die "\$text must be defined.";
    }
    
    my $self = Eg::Node->new(Eg::Node->TYPE_TEXT_NODE);
    
    my $value_buffer = StringBuffer->new;
    
    $value_buffer->push($text);
    
    $self->{value_buffer} = $value_buffer;
    
    return $self;
  }
  
  # Instance Methods
  method attr : string ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($self->{attributes_h}) {
      return undef;
    }
    
    my $attr = (string)$attributes_h->get($name);
    
    return $attr;
  }
  
  method set_attr : void ($name : string, $value : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    unless ($attributes_h) {
      $attributes_h = $self->{attributes_h} = Hash->new;
    }
    
    $attributes_h->set_string($name, $value);
  }
  
  method exists_attr : int ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    my $exists = 0;
    if ($attributes_h) {
      $exists = $attributes_h->exists($name);
    }
    
    return $exists;
  }
  
  method remove_attr : void ($name : string) {
    
    my $attributes_h = $self->{attributes_h};
    
    my $deleted_attr = (string)undef;
    
    if ($self->{attributes_h}) {
      $attributes_h->delete($name);
    }
  }
  
  method attributes : string[] () {
    
    my $attributes_h = $self->{attributes_h};
    
    my $attributes = (string[])undef;
    
    if ($attributes_h) {
      my $names = $attributes_h->keys;
      
      Sort->sort_string_asc($names);
      
      my $attributes_pairs_list = StringList->new;
      for my $name (@$names) {
        my $value = $attributes_h->get_string($name);
        
        $attributes_pairs_list->push($name);
        $attributes_pairs_list->push($value);
      }
      
      $attributes = $attributes_pairs_list->to_array;
    }
    else {
      $attributes = new string[0];
    }
    
    return $attributes;
  }
  
  method css : string ($name : string) {
    
    my $styles_h = $self->{styles_h};
    
    unless ($self->{styles_h}) {
      return undef;
    }
    
    my $css = (string)$styles_h->get($name);
    
    return $css;
  }
  
  method set_css : void ($name : string, $value : string) {
    
    my $styles_h = $self->{styles_h};
    
    unless ($styles_h) {
      $styles_h = $self->{styles_h} = Hash->new;
    }
    
    $styles_h->set_string($name, $value);
  }
  
  method exists_css : int ($name : string) {
    
    my $styles_h = $self->{styles_h};
    
    my $exists = 0;
    if ($styles_h) {
      $exists = $styles_h->exists($name);
    }
    
    return $exists;
  }
  
  method remove_css : void ($name : string) {
    
    my $styles_h = $self->{styles_h};
    
    my $deleted_css = (string)undef;
    
    if ($self->{styles_h}) {
      $styles_h->delete($name);
    }
  }
  
  method styles : string[] () {
    
    my $styles_h = $self->{styles_h};
    
    my $styles = (string[])undef;
    
    if ($styles_h) {
      my $names = $styles_h->keys;
      
      Sort->sort_string_asc($names);
      
      my $styles_pairs_list = StringList->new;
      for my $name (@$names) {
        my $value = $styles_h->get_string($name);
        
        $styles_pairs_list->push($name);
        $styles_pairs_list->push($value);
      }
      
      $styles = $styles_pairs_list->to_array;
    }
    else {
      $styles = new string[0];
    }
    
    return $styles;
  }
  
  method text : string () {
    my $nodes = $self->{nodes_list};
    
    my $nodes_length = $nodes->length;
    
    my $text = (string)undef;
    
    if ($nodes_length > 0) {
      my $first_child = (Eg::Node)$nodes->get(0);
      
      if ($first_child->type == &TYPE_TEXT_NODE) {
        $text = $first_child->{value_buffer}->to_string;
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      return "";
    }
    
    return $text;
  }
  
  method set_text : string ($text : string) {
    my $nodes = $self->{nodes_list};
    
    my $nodes_length = $nodes->length;
    
    if ($nodes_length > 0) {
      my $first_child = (Eg::Node)$nodes->get(0);
      
      if ($first_child->type == &TYPE_TEXT_NODE) {
        $first_child->{value_buffer}->set($text);
      }
      else {
        die "The first child is not a text node.";
      }
    }
    else {
      $self->{nodes_list} = List->new([&new_text($text)]);
    }
    
  }
  
}
